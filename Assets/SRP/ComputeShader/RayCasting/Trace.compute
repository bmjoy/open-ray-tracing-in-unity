#ifndef TRACE_COMPUTE
#define TRACE_COMPUTE

#include "../Ray.compute"
#include "../RayHit.compute"
#include "../Custom/Geometry/CustomGeometry.compute"
#include "../BVH/RayBoxIntersect.compute"

RayHit Trace(Ray ray, int _NumOfPrimitive)
{
    RayHit bestHit = CreateRayHit();

    int bvhStack[32];
    bvhStack[0] = 0;
    int bvhHead = 0;
    while (true)
    {
        if (bvhHead <= -1) {
            // No more node to traverse
            break;
        }

        int nodeIdx = bvhStack[bvhHead];
        bvhHead--;

        if (RayBoxIntersection(ray, _BVHTree[nodeIdx].min, _BVHTree[nodeIdx].max)) {
            if (_BVHTree[nodeIdx].leftIdx == 0 && _BVHTree[nodeIdx].rightIdx == 0) {
                // Leaf node
                for(int t = _BVHTree[nodeIdx].primitiveIdBegin; t <= _BVHTree[nodeIdx].primitiveIdEnd; t++)
                {
                    IntersectByGeometry(ray, bestHit, t);
                }
            }
            else
            {
                bvhHead++;
                bvhStack[bvhHead] = _BVHTree[nodeIdx].rightIdx;
                bvhHead++;
                bvhStack[bvhHead] = _BVHTree[nodeIdx].leftIdx;
            }
        }
    }
    
    return bestHit;
}

#endif //TRACE_COMPUTE